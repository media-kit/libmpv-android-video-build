# https://mesonbuild.com/External-Project-module.html

project('libx264', 'c')

mod = import('unstable-external_project')

configure_options = [
    '--prefix=@PREFIX@',
    '--disable-shared',
    '--enable-static',
    '--disable-cli',
    '--enable-pic'
]

if host_machine.cpu() == 'i686'
    # See https://github.com/android/ndk/issues/693
    configure_options += '--disable-asm'
endif

p = mod.add_project('configure',
    configure_options : configure_options,
    cross_configure_options : [
        '--host=@0@-linux-@1@'.format(host_machine.cpu_family(), host_machine.system())
    ],
    verbose: true
)

libx264_dep = p.dependency('libx264')
meson.override_dependency('libx264', libx264_dep)
